apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'android-apt'
apply plugin: 'let'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "net.kibotu.android.deviceinfo"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode commitCount()
        versionName "1.0"
        multiDexEnabled = true
        renderscriptTargetApi 23
        renderscriptSupportModeEnabled true
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        incremental true
        preDexLibraries = false
        jumboMode = false
        javaMaxHeapSize "4g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        ignoreWarnings true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/services/javax.annotation.processing.Processor'  // butterknife
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // compile project(':lib')

    // frequently used convenient methods
    compile('com.github.kibotu:common.android.utils:0.4.1') {
        exclude group: 'com.mcxiaoke.volley'
        // exclude module: 'net.kibotu.android.deviceinfo'
    }

    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:cardview-v7:24.0.0-alpha1'

    // recyclerview animations
    compile 'jp.wasabeef:recyclerview-animators:2.2.0'

    // recyclerview overscroll effect
    compile 'me.everything:overscroll-decor-android:1.0.0'

    // fonts
    compile('uk.co.chrisjenx:calligraphy:2.1.0')

    // crouton notifications
    compile('de.keyboardsurfer.android.widget:crouton:1.8.5@aar') {
        exclude group: 'com.google.android', module: 'support-v4'
    }

    // fabric
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }

    // auto resizing text views
    compile 'me.grantland:autofittextview:0.2.1'

    // fancy time calculations
    compile 'joda-time:joda-time:2.9.2'

}

def commitCount() {
    def stdout = new ByteArrayOutputStream()
    description = "git rev-list --full-history --all"
    println(description)
    exec {
        commandLine 'git', 'rev-list', '--full-history', '--all'
        standardOutput = stdout
    }
    String output = stdout.toString();
    List<String> commits = output.split("\n");
    println("Version Code: " + commits.size())
    return commits.size();
}
